
plugins {
    id "io.franzbecker.gradle-lombok" version "3.2.0"
    id "java"
    id "maven-publish"
    id "signing"
    id "com.github.ben-manes.versions" version "0.27.0"
    id "com.github.hierynomus.license" version "0.15.0"
}

apply plugin: "jacoco"

group = "de.alpharogroup"
version = "7.3-SNAPSHOT"
description = "xml-extensions"
sourceCompatibility = "1.8"

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    compile("org.apache.xmlbeans:xmlbeans:$xmlbeansVersion")
    compile("org.codehaus.jettison:jettison:$jettisonVersion")
    compile("com.thoughtworks.xstream:xstream:$xstreamVersion")
    compile("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    compile("xerces:xercesImpl:$xercesImplVersion")
    compile("org.json:json:$jsonVersion")
    compile("de.alpharogroup:file-worker:$fileWorkerVersion")
    compile("de.alpharogroup:jobj-core:$jobjCoreVersion")
    compile("de.alpharogroup:jobj-clone:$jobjCloneVersion")
    testCompile("de.alpharogroup:mystic-crypt:$mysticCryptVersion")
    testCompile("de.alpharogroup:test-objects:$testObjectsVersion")
    testCompile("de.alpharogroup:velocity-extensions:$velocityExtensionsVersion")
    testCompile("de.alpharogroup:meanbean-factories:$meanbeanFactoriesVersion")
    testCompile("de.alpharogroup:jobj-contract-verifier:$jobjContractVerifierVersion")
    testCompile("com.fasterxml.jackson.datatype:jackson-datatype-json-org:$jacksonVersion")
    testCompile("org.apache.velocity:velocity:$velocityVersion")
    testCompile("org.meanbean:meanbean:$meanbeanVersion")
    testCompile("org.testng:testng:$testngVersion")
    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-core:$mockitoCoreVersion")
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.main.allJava)
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
    javadoc.properties.failOnError = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.name}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "${rootProject.name}"
                description = "Useful extensions and utilities for handle xml or transform dtd to xsd and xslt"
                url = "https://github.com/lightblueseas/"+"${rootProject.name}"
                organization {
                    name = "Alpha Ro Group UG (haftungsbeschr√§ngt)"
                    url = "http://www.alpharogroup.de/"
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/lightblueseas/"+"${rootProject.name}"+"/issues"
                }
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "astrapi69"
                        name = "Asterios Raptis"
                    }
                }
                scm {
                    connection = "scm:git:git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    developerConnection = "scm:git:git@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    url = "git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("ossrhUsername") ?: project.property("ossrhUsername")
                password System.getenv("ossrhPassword") ?: project.property("ossrhPassword")
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

license {
    ext.year="2015"
    ext.owner="Asterios Raptis"
    header rootProject.file('src/main/resources/LICENSE.txt')
    excludes(["**/README",
              "**/README.md",
              "**/LICENSE",
              "**/NOTICE",
              "**/*.xml",
              "**/*.xsl",
              "**/*.xsd",
              "**/*.dtd",
              "**/*.html",
              "**/*.json",
              "**/*.jsp",
              "**/*.jpa",
              "**/*.sql",
              "**/*.properties",
              "**/*.bat",
              "**/*.gradle",
              "**/*.MF",
              "**/*.txt",
              "**/*.vm",
              "**/*.log",
              "**/*.map",
              "**/*.js.map",
              "**/*.tmpl",
              "**/*.js.tmpl",
              "**/*.editorconfig",
              "**/*.lombok.config",
              "src/test/resources/**",
              "src/main/resources/**",
              "out/**",
              "build/**"])
}

// workhack for license issue #76
gradle.startParameter.excludedTaskNames += "licenseMain"
gradle.startParameter.excludedTaskNames += "licenseTest"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["de/alpharogroup/xml/sax/handler/WriterHandler.class",
                              "de/alpharogroup/xml/XmlTransformation.class",
                              "de/alpharogroup/xml/json/ObjectToJsonQuietlyExtensions.class",
                              "de/alpharogroup/xml/node/NodeType.class",
                              "de/alpharogroup/xml/sax/handler/BreakParsingException.class"])
        }))
    }
}

check.dependsOn jacocoTestReport